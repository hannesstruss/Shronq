buildscript {
  ext.buildConfig = [
      'compileSdk': 28,
      'minSdk': 26,
      'targetSdk': 28,

      'version': [
          'major': 0,
          'minor': 5,
          'patch': 6,
          'build': 0,
      ],
  ]
  ext.buildConfig.version['name'] = "${buildConfig.version.major}.${buildConfig.version.minor}.${buildConfig.version.patch}"
  ext.buildConfig.version['code'] = buildConfig.version.major * 1000000 + buildConfig.version.minor * 10000 + buildConfig.version.patch * 100 + buildConfig.version.build


  ext.versions = [
      'supportLibrary': '1.0.2',
      'kotlin': [
          'lang': '1.3.21',
          'coroutines': '1.2.0-alpha-2'
      ],
      'dagger': '2.21',
      'arch': [
          'navigation': '1.0.0',
          'lifecycle': '2.0.0',
          'room': '2.1.0-alpha06'
      ],
      'playservices': [
          'fit': '16.0.1',
          'auth': '16.0.1'
      ]
  ]

  ext.deps = [
      'kotlin': [
          'stdlib': "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin.lang}",
          'coroutines': [
              'core': "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.kotlin.coroutines}",
              'rx2': "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:${versions.kotlin.coroutines}"
          ]
      ],
      'android': [
          'support': [
              'appCompat': "androidx.appcompat:appcompat:${versions.supportLibrary}",
              'constraintLayout': 'androidx.constraintlayout:constraintlayout:1.1.3'
          ],
          'arch': [
              'navigation': [
                  'fragment': "android.arch.navigation:navigation-fragment-ktx:${versions.arch.navigation}",
                  'ui': "android.arch.navigation:navigation-ui-ktx:${versions.arch.navigation}",
              ],
              'lifecycle': [
                  'viewmodel': "androidx.lifecycle:lifecycle-viewmodel:${versions.arch.lifecycle}",
                  'extensions': "androidx.lifecycle:lifecycle-extensions:${versions.arch.lifecycle}"
              ],
              'room': [
                  'runtime': "androidx.room:room-runtime:${versions.arch.room}",
                  'compiler': "androidx.room:room-compiler:${versions.arch.room}",
                  'ktx': "androidx.room:room-ktx:${versions.arch.room}",
                  'rx2': "androidx.room:room-rxjava2:${versions.arch.room}"
              ],
              'work': 'androidx.work:work-runtime-ktx:2.0.0'
          ]
      ],
      'firebase': [
          'core': 'com.google.firebase:firebase-core:16.0.3',
          'firestore': 'com.google.firebase:firebase-firestore:17.1.0'
      ],
      'rx': [
          'java': 'io.reactivex.rxjava2:rxjava:2.2.7',
          'android': 'io.reactivex.rxjava2:rxandroid:2.1.1',
          'binding': [
              'main': 'com.jakewharton.rxbinding2:rxbinding-kotlin:2.2.0'
          ],
          'relay': 'com.jakewharton.rxrelay2:rxrelay:2.1.0'
      ],
      'bugsnag': [
          'sdk': 'com.bugsnag:bugsnag-android:4.12.0'
      ],
      'gson': 'com.google.code.gson:gson:2.8.5',
      'timber': 'com.jakewharton.timber:timber:4.7.1',
      'dagger': [
          'runtime': "com.google.dagger:dagger:${versions.dagger}",
          'compiler': "com.google.dagger:dagger-compiler:${versions.dagger}"
      ],
      'playservices': [
          'fit': "com.google.android.gms:play-services-fitness:${versions.playservices.fit}",
          'auth': "com.google.android.gms:play-services-auth:${versions.playservices.auth}"
      ],
      'junit': 'junit:junit:4.12',
      'truth': 'com.google.truth:truth:0.43'
  ]

  repositories {
    google()
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:3.3.2'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin.lang}"
    classpath 'com.github.triplet.gradle:play-publisher:2.1.1'
  }
}

plugins {
  id "com.github.ben-manes.versions" version "0.21.0"
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-+]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

allprojects {
  repositories {
    google()
    jcenter()
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

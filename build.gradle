buildscript {
  ext.buildConfig = [
      'compileSdk': 27,
      'minSdk': 26,
      'targetSdk': 27,

      'version': [
          'major': 0,
          'minor': 5,
          'patch': 0,
          'build': 0,
      ],
  ]
  ext.buildConfig.version['name'] = "${buildConfig.version.major}.${buildConfig.version.minor}.${buildConfig.version.patch}"
  ext.buildConfig.version['code'] = buildConfig.version.major * 1000000 + buildConfig.version.minor * 10000 + buildConfig.version.patch * 100 + buildConfig.version.build


  ext.versions = [
      'supportLibrary': '27.1.1',
      'kotlin': '1.2.51',
      'dagger': '2.16',
      'arch': [
          'navigation': '1.0.0-alpha01',
          'lifecycle': '1.1.1',
          'room': '1.1.1-rc1'
      ],
      'playservices': [
          'fit': '15.0.1',
          'auth': '15.0.1'
      ]
  ]

  ext.deps = [
      'kotlin': [
          'stdlib': "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"
      ],
      'android': [
          'support': [
              'appCompat': "com.android.support:appcompat-v7:${versions.supportLibrary}",
              'constraintLayout': 'com.android.support.constraint:constraint-layout:1.1.0'
          ],
          'arch': [
              'navigation': [
                  'fragment': "android.arch.navigation:navigation-fragment-ktx:${versions.arch.navigation}",
                  'ui': "android.arch.navigation:navigation-ui-ktx:${versions.arch.navigation}",
              ],
              'lifecycle': [
                  'viewmodel': "android.arch.lifecycle:viewmodel:${versions.arch.lifecycle}",
                  'extensions': "android.arch.lifecycle:extensions:${versions.arch.lifecycle}"
              ],
              'room': [
                  'runtime': "android.arch.persistence.room:runtime:${versions.arch.room}",
                  'compiler': "android.arch.persistence.room:compiler:${versions.arch.room}",
                  'rx2': "android.arch.persistence.room:rxjava2:${versions.arch.room}"
              ],
              'work': 'android.arch.work:work-runtime-ktx:1.0.0-alpha04'
          ]
      ],
      'firebase': [
          'core': 'com.google.firebase:firebase-core:15.0.2',
          'firestore': 'com.google.firebase:firebase-firestore:16.0.0'
      ],
      'rx': [
          'java': 'io.reactivex.rxjava2:rxjava:2.1.13',
          'android': 'io.reactivex.rxjava2:rxandroid:2.0.2',
          'binding': [
              'main': 'com.jakewharton.rxbinding2:rxbinding-kotlin:2.1.1'
          ]
      ],
      'threetenabp': 'com.jakewharton.threetenabp:threetenabp:1.1.0',
      'bugsnag': [
          'sdk': 'com.bugsnag:bugsnag-android:4.4.0'
      ],
      'gson': 'com.google.code.gson:gson:2.8.4',
      'timber': 'com.jakewharton.timber:timber:4.7.0',
      'dagger': [
          'runtime': "com.google.dagger:dagger:${versions.dagger}",
          'compiler': "com.google.dagger:dagger-compiler:${versions.dagger}"
      ],
      'playservices': [
          'fit': "com.google.android.gms:play-services-fitness:${versions.playservices.fit}",
          'auth': "com.google.android.gms:play-services-auth:${versions.playservices.auth}"
      ]
  ]

  repositories {
    google()
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:3.2.0-beta02'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    classpath 'com.github.triplet.gradle:play-publisher:1.2.2'
  }
}

allprojects {
  repositories {
    google()
    jcenter()
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}
